///////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (C) 2015 The 8-Bit Bunch. Licensed under the Apache License, Version 1.1
// (the "License"); you may not use this file except in compliance with the License.
// You may obtain a copy of the License at <http://www.apache.org/licenses/LICENSE-1.1>.
// Unless required by applicable law or agreed to in writing, software distributed under
// the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF
// ANY KIND, either express or implied. See the License for the specific language
// governing permissions and limitations under the License.
///////////////////////////////////////////////////////////////////////////////////////////////////

// Functions we import from the main game loop. If you find there are some over there that aren't
// yet exported, modify this header then add the mechanics at the top of gameloop.pla.
include "gamelib.plh"

// Data structure definitions
include "playtype.plh"

// Global definitions
include "globalDefs.plh"

// Definition of constants for functions exported by this module
include "party.plh"

// Type groups
const TYPE_ALL   = $0100
const TYPE_EQUIP = $0101
const TYPE_USE   = $0102
const TYPE_DROP  = $0103

// Tab positions
const CHAR_WND_STAT_X = 20
const CHAR_WND_INV_X = 16

// This pointer is the root of all heap-tracked (and garbage collected) objects.
// See playtype.plh for definitions of all the datastructures and how they interconnect.
word global

// Exported functions go here. First a predef for each one, then a table with function pointers
// in the same order as the constants are defined in the the header.
predef _party_doPlayerSheet
word[] funcTbl = @_party_doPlayerSheet
byte[] strWhichItem = "\n^T032Which item?"
byte[] strYouSure = "\n^T008Are you sure (Y/N)?"

// Other global variables here

///////////////////////////////////////////////////////////////////////////////////////////////////
// Match item type to group type
def itemMatch(item, group)
  byte type

  type = item->t_type
  when group
    is TYPE_ALL
      return TRUE
      break
    is TYPE_EQUIP
      return type == TYPE_ARMOR or type == TYPE_WEAPON
    is TYPE_USE
      return type == TYPE_ITEM and item=>p_modifiers
      break
    is TYPE_DROP
    return type == TYPE_WEAPON or type == TYPE_ARMOR
      break
    otherwise
      return group == type
  wend
end
// Search item num
def itemNum(player, skip, num, select)
  word item

  item = player=>p_items
  while item and skip
    item = item=>p_nextObj
    skip--
  loop
  while item
    if itemMatch(item, select)
      if not num; return item; fin
      num--
    fin
    item = item=>p_nextObj
  loop
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Unequip item kind
def unequip(player, type, kind)
  word item

  if not kind; return; fin
  item = player=>p_items
  while item
    if item->t_type == type
      if streqi(item=>s_itemKind, kind) and item->b_flags & ITEM_FLAG_EQUIP
        item->b_flags = item->b_flags & ~ITEM_FLAG_EQUIP
        return item
      fin
    fin
    item = item=>p_nextObj
  loop
end
///////////////////////////////////////////////////////////////////////////////////////////////////
// Display inventory pane
def showInventory(player, page, height, select)
  word item
  byte s_item, n_item, n_page

  setMapWindow()
  clearWindow()
  rawDisplayStr("^T040^LInventory^L\n^T018")
  displayf1("Group Gold: %d", countGold())
  item = player=>p_items
  s_item = 0
  n_item = 0
  n_page = page * height
  if page
    while item and n_item < n_page
      item = item=>p_nextObj
      n_item++
    loop
  fin
  while item and n_item < (n_page + height)
    displayChar('\n')
    if itemMatch(item, select)
      displayf1("%c)", 'A' + s_item)
      s_item++
    fin
    rawDisplayStr("^T018")
    displayStr(item=>s_name)
    when item->t_type
      is TYPE_WEAPON
        if item->b_flags & ITEM_FLAG_EQUIP
          displayStr(" +")
        fin
        break
      is TYPE_ARMOR
        if item->b_flags & ITEM_FLAG_EQUIP
          displayStr(" *")
        fin
        break
    wend
    n_item++
    item = item=>p_nextObj
  loop
  while n_item < (n_page + height)
    displayChar('\n')
    n_item++
  loop
  return s_item
end
// Display skill value
def displaySkill(str, val, col)
  byte[16] mystr

  strncpy(@mystr, str, 15)
  mystr[1] = charToUpper(mystr[1])
  if col & 1
    rawDisplayStr("^T060")
  else
    displayChar('\n')
  fin
  displayf2("%d %s", val, @mystr)
end
// Show player data
def showPlayerSheet(num, mapHeight)
  word player, skill
  byte col, n_item

  // Count the number of players
  player = global=>p_players
  while num > 0
    player = player=>p_nextObj
    if !player; return; fin // Not that many players
    num--
  loop

  // First, display the player's name in the title bar
  showMapName(player=>s_name)

  // Show stats in the right panel
  setWindow2()
  clearWindow()
  rawDisplayStr("^T040^LStats^L\n")
  rightJustifyNum(player=>w_health, CHAR_WND_STAT_X);       rawDisplayStr("^T024Health\n")
  rightJustifyNum(player->b_intelligence, CHAR_WND_STAT_X); rawDisplayStr("^T024Intelligence\n")
  rightJustifyNum(player->b_strength, CHAR_WND_STAT_X);     rawDisplayStr("^T024Strength\n")
  rightJustifyNum(player->b_agility, CHAR_WND_STAT_X);      rawDisplayStr("^T024Agility\n")
  rightJustifyNum(player->b_stamina, CHAR_WND_STAT_X);      rawDisplayStr("^T024Stamina\n")
  rightJustifyNum(player->b_charisma, CHAR_WND_STAT_X);     rawDisplayStr("^T024Charisma\n")
  rightJustifyNum(player->b_spirit, CHAR_WND_STAT_X);       rawDisplayStr("^T024Spirit\n")
  rightJustifyNum(player->b_luck, CHAR_WND_STAT_X);         rawDisplayStr("^T024Luck\n")
  rightJustifyNum(player->b_armor, CHAR_WND_STAT_X);        rawDisplayStr("^T024Armor\n")

  // Show aquired skills in lower right panel
  setWindow3()
  clearWindow()
  rawDisplayStr("^T040^LSkills^L")
  displaySkill("Aim", player->b_aiming, 0)
  displaySkill("Fists", player->b_handToHand, 1)
  displaySkill("Dodge", player->b_dodging, 0)
  col = 1
  skill = player=>p_skills
  while skill
    displaySkill(skill=>s_name, skill->w_modValue, col)
    skill = skill=>p_nextObj
    col++
  loop
  // Next, show inventory in the main panel
  showInventory(player, 0, mapHeight, 0)
  rawDisplayStr("\n^T012E)quip, U)se, D)rop")
  return player
end

// Show player sheet and accept command
def _party_doPlayerSheet(num)
  word player, item
  word hInv, vInv
  byte n_item, n_page, i


  // Get size of inventory pane in chars
  getMapWindow(@hInv, @vInv)
  vInv = vInv / 11
  n_page = 0
  repeat
    player = showPlayerSheet(num, vInv)
    if !player; return; fin // Invalid player
    // Get a key, do something
    when getUpperKey()
      // Select another player to show
      is '1'; num = 0; n_page = 0; break
      is '2'; num = 1; n_page = 0; break
      is '3'; num = 2; n_page = 0; break
      // Equip player with weapon/armor
      is 'E'
        if showInventory(player, n_page, vInv, TYPE_EQUIP)
          rawDisplayStr(@strWhichItem)
          item = itemNum(player, vInv * n_page, getUpperKey() - 'A', TYPE_EQUIP)
          if item
            if unequip(player, item->t_type, item=>s_itemKind) <> item
              item->b_flags = item->b_flags | ITEM_FLAG_EQUIP
              calcPlayerArmor(player)
            fin
          fin
        else
          beep
        fin
        break
      // Use an item
      is 'U'
        if showInventory(player, n_page, vInv, TYPE_USE)
          rawDisplayStr(@strWhichItem)
          item = itemNum(player, vInv * n_page, getUpperKey() - 'A', TYPE_USE)
          if item
            if streqi(item=>p_modifiers=>s_name, "health")
              if player=>w_health < player=>w_maxHealth
                player=>w_health = min(player=>w_health + item=>p_modifiers=>w_modValue, player=>w_maxHealth)
                item->b_curUses++
                if item->b_curUses >= item->b_maxUses // all used up
                  removeFromList(@player=>p_items, item)
                fin
              fin
            fin
          fin
        else
          beep
        fin
        break
      // Drop an item
      is 'D'
        if showInventory(player, n_page, vInv, TYPE_DROP)
          rawDisplayStr(@strWhichItem)
          n_item = getUpperKey() - 'A'
          item = itemNum(player, vInv * n_page, n_item, TYPE_DROP)
          if item
            clearWindow()
            rawDisplayStr("^T050^LDrop^L\n")
            for i = 0 to n_item
              displayChar('\n')
            next
            rawDisplayStr("\n^T018")
            displayStr(item=>s_name)
            for i = n_item + 3 to vInv
              displayChar('\n')
            next
            rawDisplayStr(@strYouSure)
            if getYN()
              removeFromList(@player=>p_items, item)
              calcPlayerArmor(player)
            fin
          fin
        else
          beep
        fin
        break
      // Next inventory page
      is '>'
        if itemNum(player,  vInv * n_page, vInv * n_page + vInv, TYPE_ALL)
          n_page++
        fin
        break
      // Previous inventory page
      is '<'
        if n_page
          n_page--
        fin
        break
      // Other operations...
      is '!'; player=>w_health = player=>w_health + 10; break
      is '$'; addGold(10); break
      // All done
      otherwise return
    wend
  until 0
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Boilerplate module initialization code
global = getGlobals()
return @funcTbl
done
